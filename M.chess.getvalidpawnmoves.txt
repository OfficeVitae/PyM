; getValidPawnMoves() returns all moves the pawn at #prow,#pcol that moved #pmoves times can do
;                     #enpassantcol contains the enpassant column of the opponent
$=? ; initialize result
psign=sign(board(prow,pcol)) ; white pawns can only move up, the black ones down

; unlikely that a pawn has reached the final row, and remained a pawn but ok
if(prow==if(psign>0,8,1),return())
; if we get here, there's a row the pawn can move to

; non-capture moves
; 1. one step ahead
if(and(board(prow+psign,pcol)==0,isAValidMove(prow,pcol,prow+psign,pcol)),$.=(prow+psign,pcol))
; 2. two steps ahead
if(and(pmoves==0,board(prow+psign,pcol)==0,board(prow+2*psign,pcol)==0,isAValidMove(prow,pcol,prow+2*psign,pcol))
  ,$.=(prow+2*psign,pcol))

; capture moves
; 3. to the 'left'
if(and(pcol>1,sign(board(prow+psign,pcol-1))+psign==0,isAValidMove(prow,pcol,prow+psign,pcol-1))
  ,$.=(prow+psign,pcol-1))
; 4. to the 'right'
if(and(pcol<8,sign(board(prow+psign,pcol+1))+psign==0,isAValidMove(prow,pcol,prow+psign,pcol+1))
  ,$.=(prow+psign,pcol+1))
; 5. en passant capture
if(and(defined(enpassantcol),prow==if(psign>0,5,4))
  ,if(pcol-1==enpassantcol,$.=(prow+psign,pcol-1),if(pcol+1==enpassantcol,$.=(prow+psign,pcol+1))))
